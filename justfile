# Bazel-driven equivalents of the recipes below. Invoke as `just bazel <recipe>`.
mod bazel 'bazel.just'

# List available recipes, including nested modules (default).
default:
    @just --list --list-submodules

build:
    cargo build --release

test:
    cargo nextest run --release

test-honk:
    cargo nextest run --release -p honk

build-honk-assets: honc-cold-138-asset hoonc-octs-type-138-asset

honc-cold-138-asset:
    mkdir -p assets target/honk-assets
    target/release/honk --new --dump-wrapper-assets target/honk-assets/wrapper-assets --prelude hoon/common/hoon.hoon hoon
    cp target/honk-assets/wrapper-assets/honc-cold-138.jam assets/honc-cold-138.jam

hoonc-octs-type-138-asset:
    mkdir -p crates/honk/assets target/honk-assets
    target/release/hoonc --dynock-typed --output target/honk-assets/data-import-typed-dynock.jam hoon/probes/hoon-compiler/hoonc_octs_type_probe.hoon hoon
    target/release/extract-hoonc-octs-type target/honk-assets/data-import-typed-dynock.jam crates/honk/assets/hoonc-octs-type-138.jam

build-kernel-assets: build dumb-jam wal-jam miner-jam peek-jam bridge-jam roswell-jam

dumb-jam:
    mkdir -p assets
    target/release/hoonc --output dumb.jam hoon/apps/dumbnet/outer.hoon hoon
    mv dumb.jam assets/dumb.jam

wal-jam:
    mkdir -p assets
    target/release/hoonc --output wal.jam hoon/apps/wallet/wallet.hoon hoon
    mv wal.jam assets/wal.jam

miner-jam:
    mkdir -p assets
    target/release/hoonc --output miner.jam hoon/apps/dumbnet/miner.hoon hoon
    mv miner.jam assets/miner.jam

peek-jam:
    mkdir -p assets
    target/release/hoonc --output peek.jam hoon/apps/peek/peek.hoon hoon
    mv peek.jam assets/peek.jam

bridge-jam:
    mkdir -p assets
    target/release/hoonc --output bridge.jam hoon/apps/bridge/bridge.hoon hoon
    mv bridge.jam assets/bridge.jam

roswell-jam:
    mkdir -p assets
    target/release/hoonc --output roswell.jam hoon/apps/roswell/roswell.hoon hoon
    mv roswell.jam assets/roswell.jam

honk-roswell-kernel:
    mkdir -p assets/native
    cargo run --release -p honk --bin honk -- --new --output assets/native/roswell.jam --prelude hoon/common/hoon.hoon hoon/apps/roswell/roswell.hoon hoon

# Build every kernel in assets/ natively with honk into assets/native/.
# Never touches the hoonc-built reference jams in assets/.
honk-kernel-jams:
    cargo build --release -p honk
    mkdir -p assets/native
    target/release/honk --new --output assets/native/dumb.jam --prelude hoon/common/hoon.hoon hoon/apps/dumbnet/outer.hoon hoon
    target/release/honk --new --output assets/native/wal.jam --prelude hoon/common/hoon.hoon hoon/apps/wallet/wallet.hoon hoon
    target/release/honk --new --output assets/native/miner.jam --prelude hoon/common/hoon.hoon hoon/apps/dumbnet/miner.hoon hoon
    target/release/honk --new --output assets/native/peek.jam --prelude hoon/common/hoon.hoon hoon/apps/peek/peek.hoon hoon
    target/release/honk --new --output assets/native/bridge.jam --prelude hoon/common/hoon.hoon hoon/apps/bridge/bridge.hoon hoon
    target/release/honk --new --output assets/native/roswell.jam --prelude hoon/common/hoon.hoon hoon/apps/roswell/roswell.hoon hoon

# Compare every honk-built kernel against the hoonc-built reference.
# PASS requires byte equality or a dir-hash-only difference (proven by
# substitution + rejam). See jam-diff --kernel-parity.
honk-parity:
    cargo build --release -p honk-tools
    target/release/jam-diff --kernel-parity assets/dumb.jam assets/native/dumb.jam
    target/release/jam-diff --kernel-parity assets/wal.jam assets/native/wal.jam
    target/release/jam-diff --kernel-parity assets/miner.jam assets/native/miner.jam
    target/release/jam-diff --kernel-parity assets/peek.jam assets/native/peek.jam
    target/release/jam-diff --kernel-parity assets/bridge.jam assets/native/bridge.jam
    target/release/jam-diff --kernel-parity assets/roswell.jam assets/native/roswell.jam

# Gate: honk must compile the roswell kernel in under 60 seconds
# (cargo build excluded). Diagnose failures with NATIVE_HOON_TRACE=1 and
# RUST_LOG=honk=info for per-phase timing.
honk-roswell-timed:
    cargo build --release -p honk
    mkdir -p assets/native
    bash -c 'start=$(date +%s); target/release/honk --new --output assets/native/roswell.jam --prelude hoon/common/hoon.hoon hoon/apps/roswell/roswell.hoon hoon; end=$(date +%s); elapsed=$((end-start)); echo "roswell native compile: ${elapsed}s"; test "$elapsed" -lt 60'
